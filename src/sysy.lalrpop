use crate::ast::*;
// lalrpop里面的约定
grammar;

// 约束lexer的行为
match {
  r"\s*" => {}, // 空白符
  r"//[^\n\r]*[\n\r]*" => {}, // 注释
  r"\/\*([^\*]|\*+[^\/\*])*\*+\/" => {}, // 多行注释
  "if", // get full precedence
  "else",
  "return",
  "<=",
  ">=",
  "!=",
  "==",
  "&&",
  "||",
} else {
  _ // 剩下的情况
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <block_items: (<BlockItem>)*> "}" => Block { <> };

BlockItem: BlockItem = {
  <stmt: Stmt> => BlockItem::Stmt(stmt),
  <decl: Decl> => BlockItem::Decl(decl),
}

Stmt: Stmt = {
  <closed_stmt: ClosedStmt> => Stmt::ClosedStmt(closed_stmt),
  <open_stmt: OpenStmt> => Stmt::OpenStmt(open_stmt),
}

ClosedStmt: ClosedStmt = {
  "if" "(" <exp: Exp> ")" <closed_stmt1: ClosedStmt> "else" <closed_stmt2: ClosedStmt> => ClosedStmt::IfStmt(exp, Box::new(closed_stmt1), Box::new(closed_stmt2)),
  <non_if_stmt: BasicStmt> => ClosedStmt::BasicStmt(non_if_stmt),
  "while" "(" <exp: Exp> ")" <closed_stmt: ClosedStmt> => ClosedStmt::WhileStmt(exp, Box::new(closed_stmt)),
}

OpenStmt: OpenStmt = {
  "if" "(" <exp: Exp> ")" <closed_stmt: ClosedStmt> "else" <open_stmt: OpenStmt> => OpenStmt::IfStmtMitElse(exp, closed_stmt, Box::new(open_stmt)),
  "if" "(" <exp: Exp> ")" <stmt: Stmt> => OpenStmt::IfStmtNoElse(exp, Box::new(stmt)),
  "while" "(" <exp: Exp> ")" <open_stmt: OpenStmt> => OpenStmt::WhileStmt(exp, Box::new(open_stmt)),
}

BasicStmt: BasicStmt = {
  "return" <exp: Exp> ";" => BasicStmt::Return(exp),
  <l_val: LVal> "=" <exp: Exp> ";" => BasicStmt::Assgn(l_val, exp),
  <block: Block> => BasicStmt::Block(block),
  ";" => BasicStmt::Exp(None),
  <exp: Exp> ";" => BasicStmt::Exp(Some(exp)),
  "break" ";" => BasicStmt::Break,
  "continue" ";" => BasicStmt::Continue,
};


Exp : Exp = <l_or_exp: LOrExp> => Exp { <> };

PrimaryExp : PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new(exp)),
  <l_val: LVal> => PrimaryExp::LVal(l_val),
  <num: Number> => PrimaryExp::Number(num),
}


UnaryExp : UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(Box::new(primary_exp)),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::UnaryExp(unary_op, Box::new(unary_exp)),
}

UnaryOp : UnaryOp = {
  "+" => UnaryOp::Add,
  "-" => UnaryOp::Sub,
  "!" => UnaryOp::Rev,
}

MulOp : MulOp = {
  "*" => MulOp::Mul,
  "/" => MulOp::Div,
  "%" => MulOp::Mod,
}

AddOp : AddOp = {
  "+" => AddOp::Add,
  "-" => AddOp::Sub,
}

MulExp : MulExp = {
  <unary_exp: UnaryExp> => MulExp::UnaryExp(unary_exp),
  <mul_exp: MulExp> <mul_op: MulOp> <unary_exp: UnaryExp> =>
    MulExp::MulExp(Box::new(mul_exp), mul_op, unary_exp),
}

AddExp : AddExp = {
  <mul_exp: MulExp> => AddExp::MulExp(mul_exp),
  <add_exp: AddExp> <add_op: AddOp> <mul_exp: MulExp> =>
    AddExp::AddExp(Box::new(add_exp), add_op, mul_exp),
}

RelOp : RelOp = {
  "<" => RelOp::Lt,
  "<=" => RelOp::Le,
  ">" => RelOp::Gt,
  ">=" => RelOp::Ge,
}

RelExp : RelExp = {
  <add_exp: AddExp> => RelExp::AddExp(add_exp),
  <rel_exp: RelExp> <rel_op: RelOp> <add_exp: AddExp> =>
    RelExp::RelExp(Box::new(rel_exp), rel_op, add_exp),
}

EqOp : EqOp = {
  "==" => EqOp::Eq,
  "!=" => EqOp::Ne,
}

EqExp : EqExp = {
  <rel_exp: RelExp> => EqExp::RelExp(rel_exp),
  <eq_exp: EqExp> <eq_op: EqOp> <rel_exp: RelExp> =>
    EqExp::EqExp(Box::new(eq_exp), eq_op, rel_exp),
}

LAndOp : LAndOp = "&&" => LAndOp::And;

LAndExp : LAndExp = {
  <eq_exp: EqExp> => LAndExp::EqExp(eq_exp),
  <l_and_exp: LAndExp> <l_and_op: LAndOp> <eq_exp: EqExp> =>
    LAndExp::LAndExp(Box::new(l_and_exp), l_and_op, eq_exp),
}

LOrOp : LOrOp = "||" => LOrOp::Or;

LOrExp : LOrExp = {
  <l_and_exp: LAndExp> => LOrExp::LAndExp(l_and_exp),
  <l_or_exp: LOrExp> <l_or_op: LOrOp> <l_and_exp: LAndExp> =>
    LOrExp::LOrExp(Box::new(l_or_exp), l_or_op, l_and_exp),
}


Number: Number = <int_const: INT_CONST> => Number::IntConst(int_const);

Decl: Decl = {
  <const_decl: ConstDecl> => Decl::ConstDecl(const_decl),
  <var_decl: VarDecl> => Decl::VarDecl(var_decl),
};

ConstDecl: ConstDecl = "const" <btype: BType> <const_def_head: ConstDef> 
  <const_def_tail: ("," <ConstDef>)*> ";" => ConstDecl {
    btype, 
    const_defs: vec![const_def_head].into_iter().chain(const_def_tail).collect(),
  };

VarDecl: VarDecl = <btype: BType> <var_def_head: VarDef>
  <var_def_tail: ("," <VarDef>)*> ";" => VarDecl {
    btype,
    var_defs: vec![var_def_head].into_iter().chain(var_def_tail).collect(),
  };

BType: BType = "int" => BType::Int;

ConstDef: ConstDef = <ident: Ident> "=" <const_init_val: ConstInitVal> => 
  ConstDef { <> };

ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal { <> };

VarDef: VarDef = {
  <ident: Ident> "=" <var_init_val: InitVal> => VarDef::VarDefInit(<>),
  <ident: Ident> => VarDef::VarDefUninit(<>),
  };

LVal: LVal = <ident: Ident> => LVal { <> };

InitVal: InitVal = <exp: Exp> => InitVal { <> };

ConstExp: ConstExp = <exp: Exp> => ConstExp { <> };

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
INT_CONST: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();
